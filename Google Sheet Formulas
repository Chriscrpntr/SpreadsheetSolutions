Filtering Multiple Ranges For Substring

=ifna(FILTER(FLATTEN(A1:A7),SEARCH("laund",FLATTEN(A1:A7))),"")

Vlookup Nested Ifs

=IFERROR(VLOOKUP(Playground!B19,{10,"Banana";20,"Orange";30,"Grape"},2,False))

For Only Equals

=IFERROR(VLOOKUP(Playground!B19,{10,"Banana";20,"Orange";30,"Grape"},2,True))
For Greater than or equal to till next condition

Lambda Ifs

=ArrayFormula( Map(F27:F48, Lambda( input, If( input > 100, "Large", If(input > 50, "Medium", "Small") ) ) ) )

Sum Unique Items

=QUERY( {A1:B}, "select Col1, Sum(Col2) where Col1 is not null group by Col1 label Col1 'Item', Sum(Col2) 'Total'")

  For Adding Array's of multiple ranges 
  =ArrayFormula(QUERY({Q3:T22;W3:Z22;AC3:AF22;AI3:AL22;AO3:AR22;AU3:AX22;BA3:BD22;BG3:BJ22;BM3:BP22;BS3:BV22;BY3:CB22;CE3:CH22}, "select Col1, Sum(Col2),Sum(Col3),Sum(Col3)/Sum(Col2) where Col1 is not null group by Col1 label Col1 'SKU', Sum(Col2) 'Quantity', Sum(Col3) 'Total Price', Sum(Col3)/Sum(Col2) 'Average Price'"))

Recursive Lambda Example

=let(lamm,lambda(lamm,a,if(a<10,lamm(lamm,a+1),a)),lamm(lamm,1))

Find Unichar of all non-letter,non-number,non-quote,non-hyphen characters in string

=UNIQUE(let(letters,REGEXREPLACE(A1, "[a-zA-Z0-9]|['"&Char(34)&"-]",""),map(sequence(LEN(letters)),lambda(a,CODE(mid(letters,a,1))))))
